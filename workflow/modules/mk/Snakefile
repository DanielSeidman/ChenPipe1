configfile: "config/config.yaml"
include: "common.smk"


ref_name = samples['ref_name'].unique().tolist()

rule all:
    input:
        expand("results/{ref_name}/mk/{prefix}_mk_table.tsv", ref_name=ref_name, prefix=config['final_prefix'])

rule prep_genome:
    """
    Gets the needed information (fasta and gff) from NCBI
    """
    input: 
        ref = get_ref,
        gff = get_gff
    output:
        ref = "results/{ref_name}/mk/{ref_name}.fna",
        gff = "results/{ref_name}/mk/{ref_name}.gff"
    params:
        dataset = "results/{ref_name}/mk/{ref_name}_dataset.zip",
        outdir = "results/{ref_name}/mk/{ref_name}"
    conda:
        "envs/ncbi.yml"
    shell:
        """
        set +e
        #if genome is local, datasets will fail, we will just continue
        mkdir -p {params.outdir}
        datasets download genome accession  --exclude-protein --exclude-rna --filename {params.dataset} {wildcards.ref_name} \
        && 7z x {params.dataset} -aoa -o{params.outdir}

        if [ -z "{input.ref}" ]
        then
            cat {params.outdir}/ncbi_dataset/data/{wildcards.ref_name}/*.fna > {output.ref}
        else
            cp {input.ref} {output.ref}
        fi

       if [ -z "{input.gff}" ]
        then
            cp {params.outdir}/ncbi_dataset/data/{wildcards.ref_name}/genomic.gff {output.gff}
        else
            cp {input.gff} {output.gff}
        fi
        """

rule split_samples:
    """
    Splits sample sheet to make ingroup and outgroup files
    """
    output:
        exclude = "results/{ref_name}/mk/{prefix}_exclude.txt",
        outgroups = "results/{ref_name}/mk/{prefix}_ougroups.txt"
    run:
        out_df = samples[["BioSample", "SampleType"]]
        out_df.drop_duplicates("BioSample", inplace=True)
        exclude =out_df[~out_df.SampleType.isin(["ingroup", "outgroup"])].BioSample
        outgroups = out_df[out_df.SampleType.isin(["outgroup"])].BioSample
        exclude.to_csv(output[0], index=False, sep="\t", header=False)
        outgroups.to_csv(output[1], index=False, sep="\t", header=False)

rule degenotate:
    """
    Runs degenotate to compute MK tables
    """
    input:
        vcf = "results/{ref_name}/{prefix}_clean_snps.vcf.gz",
        genome = "results/{ref_name}/mk/{ref_name}.fna",
        gff = "results/{ref_name}/mk/{ref_name}.gff",
        exclude = "results/{ref_name}/mk/{prefix}_exclude.txt",
        outgroups = "results/{ref_name}/mk/{prefix}_ougroups.txt"
    output:
        "results/{ref_name}/mk/{prefix}_mk_table.tsv"
    params:
        delim = "space"
    log:
        "logs/{ref_name}/mk/{prefix}_degenotate.txt"
    conda:
        "envs/mk.yml"
    shell:
        """
        if [ -s {input.exclude} ]
        then
            degenotate.py --overwrite -a {input.gff} -g {input.genome} -u {input.outgroups} -e {input.exclude} -d {params.delim} -o "results/{wildcards.ref_name}/mk/{wildcards.prefix}_degen_raw" -v {input.vcf}
        else
            degenotate.py --overwrite -a {input.gff} -g {input.genome} -u {input.outgroups} -d {params.delim} -o "results/{wildcards.ref_name}/mk/{wildcards.prefix}_degen_raw" -v {input.vcf}
        fi
        cp results/{wildcards.ref_name}/mk/{wildcards.prefix}_degen_raw/mk.tsv {output}
        """
